buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.10.1"
        classpath "gradle.plugin.com.palantir.gradle.gitversion:gradle-git-version:0.11.0"
    }
}

apply plugin: 'groovy'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'com.palantir.git-version'
apply plugin: 'maven-publish'

group = 'eu.emundo'
description = "Gradle Plugin for Octane Model Generator"
version = gitVersion()

println ''
println '###############################################'
println '#'
println "# ${description} $version"
println '#'
println '###############################################'
println ''

repositories {
    if (project.hasProperty("mavenRepository")) {
        maven {
            url "$mavenRepository"
            credentials {
                username "$mavenUsername"
                password "$mavenPassword"
            }
        }
    }
    jcenter()
    mavenLocal()
}

def sdkVersion = "12.60.16"

if (project.hasProperty("octaneSdkVersion")) {
    sdkVersion = octaneSdkVersion
    logger.info("sdkVersion: $octaneSdkVersion")
}


dependencies {
    compile gradleApi()
    compile localGroovy()

    compile group: 'com.microfocus.adm.octane.sdk', name: 'sdk-generate-entity-models-maven-plugin', version: "$sdkVersion"
}

pluginBundle {
    website = 'https://github.com/emundo/octane-gradle-plugin'
    vcsUrl = 'https://github.com/emundo/octane-gradle-plugin.git'

    plugins {
        generateModelsPlugin {
            tags = ['octane', 'alm', 'models', 'entity', 'generator']
            description = 'Gradle Plugin for Octane Model Generator.'
            id = 'eu.emundo.octane'
            displayName = 'Gradle Octane Model Generator plugin'
        }
    }
}


publishing {
    publications {
        maven(MavenPublication) {
            groupId 'eu.emundo'
            artifactId 'eu.emundo.octane'
            from components.java
            version "$version"
        }
    }
}
