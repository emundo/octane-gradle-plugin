plugins {
    id "com.palantir.git-version" version "0.12.3"
    id "com.gradle.plugin-publish" version "0.12.0"
    id "com.diffplug.spotless" version "5.7.0"
}

apply plugin: 'groovy'
apply plugin: "com.gradle.plugin-publish"
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.diffplug.spotless'

group = 'eu.emundo'
description = "Gradle Plugin for Octane Model Generator"
version = gitVersion()

println ''
println '###############################################'
println '#'
println "# ${description} $version"
println '#'
println '###############################################'
println ''

repositories {
    if (project.hasProperty("mavenRepository")) {
        maven {
            url "$mavenRepository"
            credentials {
                username "$mavenUsername"
                password "$mavenPassword"
            }
        }
    }
    jcenter()
    mavenLocal()
}

def sdkVersion = "15.1.40"

if (project.hasProperty("octaneSdkVersion")) {
    sdkVersion = octaneSdkVersion
    logger.info("sdkVersion: $octaneSdkVersion")
}


dependencies {
    compile gradleApi()
    compile localGroovy()

    compile group: 'com.microfocus.adm.almoctane.sdk', name: 'sdk-src', version: "$sdkVersion"
    compile group: 'org.apache.velocity', name: 'velocity', version: '1.7'
}

pluginBundle {
    website = 'https://github.com/emundo/octane-gradle-plugin'
    vcsUrl = 'https://github.com/emundo/octane-gradle-plugin.git'

    plugins {
        generateModelsPlugin {
            tags = ['octane', 'alm', 'models', 'entity', 'generator']
            description = 'Gradle Plugin for Octane Model Generator.'
            id = 'eu.emundo.octane.generator'
            displayName = 'Gradle Octane Model Generator plugin'
        }
    }
}

// Formatting
spotless {
    java {
        removeUnusedImports()
        trimTrailingWhitespace()
        importOrderFile "${project.rootDir}/importorder.properties"
        eclipse('4.8.0').configFile "${project.rootDir}/eclipse-formatter.xml"
    }
    format 'misc', {
        target project.fileTree(project.rootDir) {
            include '**/*.gradle', '**/.gitignore', '*.yml', '**/*.xml'
            exclude '**/.idea/*'
        }

        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }
}
